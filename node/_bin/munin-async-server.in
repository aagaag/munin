#! @@PERL@@
# -*- cperl -*-
#
# Copyright (C) 2010 Steve Schnepp
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; version 2 dated June,
# 1991.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
#
#

use strict;
use warnings;

use IO::Socket;
use File::Path qw(make_path);
use Getopt::Long;
use Pod::Usage;

my $host = "localhost:4949";
my $SPOOLDIR = "/tmp/munin-async-spool";
my $verbose;
my $debug;
my $help;

GetOptions(
	"host=s" => \$host,
	"spooldir|s=s" => \$SPOOLDIR,

	"help|h" => \$help,
	"verbose|v" => \$verbose,
	"debug" => \$debug,
) or pod2usage(1);
if ($help) {
	pod2usage(1);
}

unless (-d $SPOOLDIR) {
	make_path($SPOOLDIR, { verbose => $verbose, } ) 
		or die ("Cannot create '$SPOOLDIR': $!");
}


# Read whole configuration
my @plugins;
my %plugins_rate;
{
	print STDERR "Reading config from $host\n" if $verbose;
	my $sock = new IO::Socket::INET( 
		PeerAddr	=> "$host", 
		Proto	=> 'tcp'
	) || die "Error creating socket: $!"; 

	print $sock "list\n";
	<$sock>; # Read the first header comment line
	my $plugins_line = <$sock>;
	open(OUTFILE, "> $SPOOLDIR/munin-daemon.list");
	print OUTFILE $plugins_line;
	close(OUTFILE);
	chomp($plugins_line);
	@plugins = split(/ /, $plugins_line);

	# search update_rate for each plugin
	foreach my $plugin (@plugins) {
		print STDERR "Asking config for $plugin\n" if $verbose;
		$plugins_rate{$plugin} = 300; # default : 5 min
		print $sock "config $plugin\n";
		open (OUTFILE, "> $SPOOLDIR/munin-daemon.$plugin.config");
		while(my $line = <$sock>) {
			print OUTFILE $line;
			if ($line =~ m/^update_rate (\d+)/) {
				# The plugin has a special update_rate: overriding it
				# XXX - Doesn't take into account a per field update_rate
				$plugins_rate{$plugin} = $1;
			}
			if ($line =~ m/^\./) {
				# Starting with . => end
				last;
			}
		}
		close (OUTFILE);
	}
}

# now, update regularly...
# ... but each plugin in its own process to avoid delay-leaking
foreach my $plugin (@plugins) {
	fetch_data($plugin, $plugins_rate{$plugin});
}

# wait... forever
while(1) { sleep 100000; }

sub fetch_data
{
	my $plugin = shift;
	my $plugin_rate = shift;

	# spawn a child
	if (fork()) {
		# parent, return directly	
		return;
	}

	# Setting the command name for a useful top information
	$0 .= " [$plugin]";

	my $last_updated = 0;
	while(1) {
		my $when = time;
		print STDERR "[$$][$plugin] checking\n" if $debug;
		if ($when < $last_updated + $plugin_rate) {
			# not yet, sleep for 1s until next step
			sleep 1;
			next;
		}
		$last_updated = $when;

		my $sock = new IO::Socket::INET( 
			PeerAddr	=> "$host", 
			Proto	=> 'tcp'
		) || die "Error creating socket: $!"; 
		<$sock>; # skip header

		print $sock "fetch $plugin\n";
		
		open (OUTFILE, ">> $SPOOLDIR/munin-daemon.$plugin.data");
		while(my $line = <$sock>) {
			print STDERR "[sock] $line" if $debug;
			if ($line =~ m/^\./) {
				# Starting with . => end
				last;
			}

			if ($line =~ m/^(\w+)\.value (\d+):(-?\d+\.?\d*)/) {
				# the plugin already outputed a time
				# keeping it
				print OUTFILE "$1.value $2:$3\n";
				print STDERR "[$plugin] $1.value $2:$3\n" if $debug;
			} elsif ($line =~ m/^(\w+)\.value (-?\d+\.?\d*)/) {
				# the plugin doesn't ouput a time, 
				# using now()
				print OUTFILE "$1.value $when:$2\n";
				print STDERR "[$plugin] $1.value $when:$2\n" if $debug;
			}
		}
		close (OUTFILE);
	}
}

__END__

=head1 NAME

munin-async-server - A program to spool munin-node calls

=head1 SYNOPSIS

munin-async-server [options]

 Options:
     --host <hostname:port>     Connect to this munin-node [localhost:4949]
     -s --spool <spooldir>      Store the spooled data in this dir [/tmp/munin-async-spool]

     -v --verbose               Be verbose
     -h --help                  View this message	
