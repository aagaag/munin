#!@@PERL@@ -w
# -*- perl -*-
# vim: ft=perl

=head1 NAME

snmp__tcpCurrEstab - Munin plugin to monitor the number of established
TCP connections on an SNMP device.

=head1 APPLICABLE SYSTEMS

Any SNMP-capable device that reports the TCP-MIB::tcpCurrEstab.0 OID. 

=head1 CONFIGURATION

As a rule SNMP plugins need site specific configuration.  The default
configuration (shown here) will only work on insecure sites/devices.

   [snmp_*]
	env.version 2
        env.community public

In general SNMP is not very secure at all unless you use SNMP version
3 which supports authentication and privacy (encryption).  But in any
case the community string for your devices should not be "public".

Please see 'perldoc Munin::Plugin::SNMP' for further configuration
information.

=head1 INTERPRETATION

The number of TCP connections to the device for which the current state
is either ESTABLISHED or CLOSE-WAIT.

=head1 MIB INFORMATION

This plugin requires support for the TCP-MIB (RFC 4022).  It reports the
contents of the tcpCurrEstab OID.

=head1 MAGIC MARKERS

  #%# family=snmpauto
  #%# capabilities=snmpconf

=head1 VERSION

  $Id$

=head1 BUGS

None known.

=head1 AUTHOR

Copyright (C) 2010 Aleksandar Lazic

Documented and updated to use Munin::Plugin::SNMP by Matthew Boyle.

=head1 LICENSE

GPLv2.

=cut


use strict;
use Munin::Plugin::SNMP;

if (defined $ARGV[0] and $ARGV[0] eq 'snmpconf') {
	# .iso.org.dod.internet.mgmt.mib-2.tcp.tcpCurrEstab.0
	print "require 1.3.6.1.2.1.6.9.0 [0-9]\n";
	exit 0;
}

if (defined $ARGV[0] and $ARGV[0] eq "config") {
	my ($host) = Munin::Plugin::SNMP->config_session();

	print "host_name $host\n" unless ($host eq 'localhost');
	print <<"EOC";
graph_title Number of currently established tcp connections 
graph_args --base 1000 -l 0 
graph_vlabel Number of tcp connections
graph_category network
graph_info This graph shows the number of current established TCP connections
tcpcon.label connections
tcpcon.draw LINE2
tcpcon.info The number of tcp connections.
EOC
	exit 0;
}

my $session = Munin::Plugin::SNMP->session();
print "tcpcon.value ", $session->get_single('1.3.6.1.2.1.6.9.0'), "\n";

