#!@@PERL@@ -w
# -*- perl -*-

=head1 NAME

bind9_rndc - Plugin to monitor usage of bind 9 servers using rndc stats

=head1 CONFIGURATION

The following environment variables are used by this plugin

 rndc      - location of the rndc command.
             set to empty if the stats are updated otherwise
 querystats - location of the statistics file

=head1 USAGE

To intall, put

 statistics-file "/var/run/named.stats";

in the options part of your named.conf or set the querystats variable
(see below) to where your named puts the statistics file by default.

You must also make sure the rndc.key file is readable by the plugin.

=head1 AUTHOR

Contributed by Laurent Facq 15/06/2004
Based on Nicolai Langfeldt bind9 plugin
Reworked by Dagfinn Ilmari Manns√•ker

=head1 LICENSE

License unknown

=head1 MAGIC MARKERS

 #%# family=contrib

=cut

use strict;

my $rndc = defined($ENV{rndc}) ? $ENV{rndc} : '/usr/sbin/rndc';
my $querystats = $ENV{querystats} || '/var/run/named.stats';
my %IN;

system("$rndc stats") if $rndc;
    
open(my $stats, '<', $querystats) or die "$querystats: $!\n";
seek($stats , -400, 2);		# go nearly to the end of the file
				# to avoid reading it all

while (my $line = <$stats>) {
    chomp $line;

    # We want the last block like this in the file
    #+++ Statistics Dump +++ (1087277501)
    #success 106183673
    #referral 2103636
    #nxrrset 43534220
    #nxdomain 47050478
    #recursion 37303997
    #failure 17522313
    #--- Statistics Dump --- (1087277501)
	
    if ($line =~ m/\+\+\+ Statistics Dump \+\+\+/) {
	# reset
	undef %IN;
    } else {
	my ($what, $nb)= split('\s+', $line);
	if ($what && ($what ne '---')) {
	    $IN{$what} = $nb;
	}
    }
}
close($stats);

if (defined($ARGV[0]) and ($ARGV[0] eq 'config')) {
    print "graph_title DNS Queries by status\n";
    print "graph_vlabel queries / \${graph_period}\n";
    print "graph_category BIND\n";

    for my $key (keys %IN) {
	print "query_$key.label $key\n";
	print "query_$key.type DERIVE\n";
	print "query_$key.min 0\n";
    }
} else {
    print "query_$_.value $IN{$_}\n" for keys %IN;
}
